initialize() {
	initializeMutationRate(1e-8);
	defineConstant("K", 2000);
	initializeMutationType("m1", 0.0, "f", 0.001);
	m1.convertToSubstitution = T;
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 999999);
	initializeRecombinationRate(1e-6);
	initializeSex("A");
}

1 {
	sim.addSubpop("p1", K);
	print("Generation, FixedMutations, NucleotideHeterozygosity");
}

10000: late() {
	if (sim.generation % 100 == 0)
	{
		inds = sim.subpopulations.individuals;
		fixedCount = sim.substitutions.size();

		// find the average heterozygosity across all individuals
		total = 0.0 ;

		for (ind in inds)
		{
	 		total = total + calcPairHeterozygosity(ind.genome1, ind.genome2);
		}
		pi = total / inds.size();

		catn(sim.generation + " " + fixedCount + " " + pi);
	}
}

50000 late() {
sim.simulationFinished();
}
